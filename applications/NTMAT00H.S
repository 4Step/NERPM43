; Do not change filenames or add or remove FILEI/FILEO statements using an editor. Use Cube/Application Manager.
RUN PGM=MATRIX MSG='Create SPDCAP Lookup'
FILEO PRINTO[2] = "{Scenario_Dir}\output\SPDCAP.ERR"
FILEO PRINTO[1] = "{Scenario_Dir}\output\SPDCAP.CSV"
FILEI RECI = "{Scenario_Dir}\input\SPDCAP.{YEAR}{ALT}"
ARRAY SPDLOOKUP=999999 CAPLOOKUP=999999
LAT=SUBSTR(RECI,1,2)
HAT=SUBSTR(RECI,3,2)
LFT=SUBSTR(RECI,5,2)
HFT=SUBSTR(RECI,7,2)
LLN=SUBSTR(RECI,9,2)
HLN=SUBSTR(RECI,11,2)
CAPFUNC=SUBSTR(RECI,13,1)
CAP=SUBSTR(RECI,14,5)
SPDFUNC=SUBSTR(RECI,19,1)
SPD=SUBSTR(RECI,20,4)
    LATVAL=VAL(LAT)
    HATVAL=VAL(HAT)
    LFTVAL=VAL(LFT)
    HFTVAL=VAL(HFT)
    LLNVAL=VAL(LLN)
    HLNVAL=VAL(HLN)
    CAPVAL=VAL(CAP)
    SPDVAL=VAL(SPD)
; PLACE INITIAL CAPACITIES & SPEEDS INTO AN ARRAY
IF (CAPFUNC=' ') 
    LOOP ATYPE=LATVAL,HATVAL
      LOOP FTYPE=LFTVAL,HFTVAL
        LOOP LANES=LLNVAL,HLNVAL
        INDEXVAL=ATYPE*10000+FTYPE*100+LANES
        CAPLOOKUP[INDEXVAL]=CAPVAL  
        ENDLOOP
      ENDLOOP
    ENDLOOP
ENDIF
IF (SPDFUNC=' ') 
    LOOP ATYPE=LATVAL,HATVAL
      LOOP FTYPE=LFTVAL,HFTVAL
        LOOP LANES=LLNVAL,HLNVAL
        INDEXVAL=ATYPE*10000+FTYPE*100+LANES
        SPDLOOKUP[INDEXVAL]=SPDVAL  
        ENDLOOP
      ENDLOOP
    ENDLOOP
ENDIF
;IF (CAPFUNC='*')
IF (CAPFUNC='*'|CAPFUNC='+'|CAPFUNC='-')
    LOOP ATYPE=LATVAL,HATVAL
      LOOP FTYPE=LFTVAL,HFTVAL
        LOOP LANES=LLNVAL,HLNVAL
        INDEXVAL=ATYPE*10000+FTYPE*100+LANES
;        CAPLOOKUP[INDEXVAL]=CAPLOOKUP[INDEXVAL]*CAPVAL
        IF (CAPFUNC='*') CAPLOOKUP[INDEXVAL]=CAPLOOKUP[INDEXVAL]*CAPVAL
        IF (CAPFUNC='+') CAPLOOKUP[INDEXVAL]=CAPLOOKUP[INDEXVAL]+CAPVAL
        IF (CAPFUNC='-') CAPLOOKUP[INDEXVAL]=CAPLOOKUP[INDEXVAL]-CAPVAL
        ENDLOOP
      ENDLOOP
    ENDLOOP
ENDIF
IF (SPDFUNC='*'|SPDFUNC='+'|SPDFUNC='-')
    LOOP ATYPE=LATVAL,HATVAL
      LOOP FTYPE=LFTVAL,HFTVAL
        LOOP LANES=LLNVAL,HLNVAL
        INDEXVAL=ATYPE*10000+FTYPE*100+LANES
        IF (SPDFUNC='*') SPDLOOKUP[INDEXVAL]=SPDLOOKUP[INDEXVAL]*SPDVAL
        IF (SPDFUNC='+') SPDLOOKUP[INDEXVAL]=SPDLOOKUP[INDEXVAL]+SPDVAL
        IF (SPDFUNC='-') SPDLOOKUP[INDEXVAL]=SPDLOOKUP[INDEXVAL]-SPDVAL
        ENDLOOP
      ENDLOOP
    ENDLOOP
ENDIF


IF (I=0)
  PRINT LIST='SPEED OR CAPACITY ERRORS WHERE THE SPDCAP RESULT IS LESS THAN ZERO', PRINTO=2
  LOOP IVAL=1,999999
     IF (CAPLOOKUP[IVAL]>0|SPDLOOKUP[IVAL]>0)   PRINT CSV=T, LIST=IVAL(6.0),CAPLOOKUP[IVAL],SPDLOOKUP[IVAL],PRINTO=1
     IF (CAPLOOKUP[IVAL]<0)   
        CAPERRCNT=CAPERRCNT+1
        PRINT CSV=T, LIST='SPDCAP ERROR FOR ATFTLN=',IVAL(6.0),'  CAPACITY=',CAPLOOKUP[IVAL](9.2),PRINTO=2
     ENDIF
     IF (SPDLOOKUP[IVAL]<0)
        SPDERRCNT=SPDERRCNT+1
        PRINT CSV=T, LIST='SPDCAP ERROR FOR ATFTLN=',IVAL(6.0),'     SPEED=',SPDLOOKUP[IVAL](9.2),PRINTO=2
     ENDIF
  ENDLOOP
  PRINT LIST='\n************Error Report Summary*************',
             '\nTOTAL LESS THAN ZERO CAPACITY ERRORS=',CAPERRCNT(8.0C),
             '\nTOTAL LESS THAN ZERO SPEED ERRORS   =',SPDERRCNT(8.0C), printo=2
ENDIF

ENDRUN
