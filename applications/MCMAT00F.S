; Do not change filenames or add or remove FILEI/FILEO statements using an editor. Use Cube/Application Manager.
RUN PGM=MATRIX PRNFILE="C:\KumarA\NERPM\CUBE\MCMAT00E.PRN" MSG='NHB'
FILEI MATI[1] = "{OUTDIR}\PTRIPSMC.MAT"
FILEI MATI[10] = "C:\KumarA\NERPM\CUBE\STATMD.{alt}{Year}"
FILEI ZDATI[4] = "C:\KumarA\NERPM\cube\STATDATA.dbf"
FILEI ZDATI[3] = "C:\KumarA\NERPM\CUBE\ZONAL1.DBF"
FILEI ZDATI[2] = "C:\KumarA\NERPM\CUBE\ZACCESMD1.DBF"
FILEO MATO[1] = "C:\KumarA\NERPM\CUBE\HTNHB.MAT",
 MO=151-166,
 NAME= TOTAUTOS,DA,SHRIDE,SR1PASS,SR2PASS,TRANSIT,WALKACC,WALKLOC,WALKPRE,WLKPREB,WLKPRER,AUTOACC,AUTOLOC,AUTOPRE,AUTOPREB,AUTORAIL,
 DEC=16*S
FILEI LOOKUPI[2] = "C:\KumarA\NERPM\CUBE\MC_COEFFICIENTS.CSV"
FILEI LOOKUPI[1] = "C:\KumarA\NERPM\CUBE\MC_CONSTANTS.CSV"
FILEI ZDATI[1] = "C:\KumarA\NERPM\CUBE\ZINCOMMD1.DBF"
FILEI MATI[9] = "C:\KumarA\NERPM\CUBE\TSKIMMD6.MAT"
FILEI MATI[8] = "C:\KumarA\NERPM\CUBE\TSKIMMD5.MAT"
FILEI MATI[7] = "C:\KumarA\NERPM\CUBE\TSKIMMD4.MAT"
FILEI MATI[6] = "C:\KumarA\NERPM\CUBE\TSKIMMD3.MAT"
FILEI MATI[5] = "C:\KumarA\NERPM\CUBE\TSKIMMD2.MAT"
FILEI MATI[4] = "C:\KumarA\NERPM\CUBE\TSKIMMD1.MAT"
FILEI MATI[3] = "C:\KumarA\NERPM\CUBE\HVSKIMSMC.MAT"
FILEI MATI[2] = "C:\KumarA\NERPM\CUBE\HSKIMSMC.MAT"

MW[1]=MI.1.HBW ; NOT USED IN THIS STEP
MW[2]=MI.1.HBO ; NOT USED IN THIS STEP
MW[3]=MI.1.NHB 


;Constants
lookup, name=constants,
lookup[3]=1, result=4,
interpolate=n, , LIST=Y, lookupi=1
;K=CONSTANT (NHB)
OC2=CONSTANTS(3,1)       
OC3=CONSTANTS(3,2)       
AOC=CONSTANTS(3,3)       
TASPD=CONSTANTS(3,4)     
OCTA=CONSTANTS(3,5)      
;;WALKPATRAN=CONSTANTS(3,6)
;;WALKPAPR=CONSTANTS(3,7)  
WALKPAKR=CONSTANTS(3,8)  
K_DA_AUTO0=CONSTANTS(3,9)
K_DA_AUTO1=CONSTANTS(3,10)
K_DA_AUTO2=CONSTANTS(3,11)
K_SR1PASS_AUTO0=CONSTANTS(3,12)
K_SR1PASS_AUTO1=CONSTANTS(3,13)
K_SR1PASS_AUTO2=CONSTANTS(3,14)
K_SR2PASS_AUTO0=CONSTANTS(3,15)
K_SR2PASS_AUTO1=CONSTANTS(3,16)
K_SR2PASS_AUTO2=CONSTANTS(3,17)
K_WALKLOC_AUTO0=CONSTANTS(3,18)
K_WALKLOC_AUTO1=CONSTANTS(3,19)
K_WALKLOC_AUTO2=CONSTANTS(3,20)
K_WLKPREB_AUTO0=CONSTANTS(3,21)
K_WLKPREB_AUTO1=CONSTANTS(3,22)
K_WLKPREB_AUTO2=CONSTANTS(3,23)
K_WLKPRER_AUTO0=CONSTANTS(3,24)
K_WLKPRER_AUTO1=CONSTANTS(3,25)
K_WLKPRER_AUTO2=CONSTANTS(3,26)
K_AUTOLOC_AUTO0=CONSTANTS(3,27)
K_AUTOLOC_AUTO1=CONSTANTS(3,28)
K_AUTOLOC_AUTO2=CONSTANTS(3,29)
K_AUTOPREB_AUTO0=CONSTANTS(3,30)
K_AUTOPREB_AUTO1=CONSTANTS(3,31)
K_AUTOPREB_AUTO2=CONSTANTS(3,32)
K_AUTORAIL_AUTO0=CONSTANTS(3,33)
K_AUTORAIL_AUTO1=CONSTANTS(3,34)
K_AUTORAIL_AUTO2=CONSTANTS(3,35)


;Coefficients
lookup, name=coefficients, 
lookup[3]=1, result=4, 
interpolate=n, LIST=Y, lookupi=2
;Coefficients (NHB)
if (i=1)
TERMCOEF=COEFFICIENTS(3,1)  
AAUTORUNC=COEFFICIENTS(3,2) 
CTOLL=COEFFICIENTS(3,3)     
HOVDIFFC=COEFFICIENTS(3,4)  
ACOSTC=COEFFICIENTS(3,5)    
AUTOCINF=COEFFICIENTS(3,6)  
PARKC=COEFFICIENTS(3,7)     
AUTOINFC=COEFFICIENTS(3,8)  
PARKINFC=COEFFICIENTS(3,9)  
AU1DTC=COEFFICIENTS(3,10)   
AU1XPC=COEFFICIENTS(3,11)   
AU1XAC=COEFFICIENTS(3,12)   
AU2DTC=COEFFICIENTS(3,13)   
AU2XPC=COEFFICIENTS(3,14)   
AU2XAC=COEFFICIENTS(3,15)   
AU3DTC=COEFFICIENTS(3,16)   
AU3XPC=COEFFICIENTS(3,17)   
AU3XAC=COEFFICIENTS(3,18)   
WALKC=COEFFICIENTS(3,19)    
TRUNC=COEFFICIENTS(3,20)    
WTTAC=COEFFICIENTS(3,21)    
WTTBC=COEFFICIENTS(3,22)    
XFTC=COEFFICIENTS(3,23)     
NXFC=COEFFICIENTS(3,24)     
FAREC=COEFFICIENTS(3,25)    
FAREINFC=COEFFICIENTS(3,26) 
TWLDTC=COEFFICIENTS(3,27)   
TWLXPC=COEFFICIENTS(3,28)   
TWLXAC=COEFFICIENTS(3,29)   
TWEDTC=COEFFICIENTS(3,30)   
TWEXPC=COEFFICIENTS(3,31)   
TWEXAC=COEFFICIENTS(3,32)   
TWRDTC=COEFFICIENTS(3,33)   
TWRXPC=COEFFICIENTS(3,34)   
TWRXAC=COEFFICIENTS(3,35)   
TALDTC=COEFFICIENTS(3,36)   
TALXPC=COEFFICIENTS(3,37)   
TALXAC=COEFFICIENTS(3,38)   
TAEDTC=COEFFICIENTS(3,39)   
TAEXPC=COEFFICIENTS(3,40)   
TAEXAC=COEFFICIENTS(3,41)   
TARDTC=COEFFICIENTS(3,42)   
TARXPC=COEFFICIENTS(3,43)   
TARXAC=COEFFICIENTS(3,44)   

MW[141]=0
MW[142]=0
MW[143]=0
MW[144]=0
MW[145]=0
MW[146]=0
MW[147]=0
MW[148]=0
MW[149]=0

DA_NESTCOEF=  1.0 * 0.8
SR_NESTCOEF=  0.2 * 0.8
WLB_NESTCOEF= 1.0 * 0.5 * 0.3
WPR_NESTCOEF= 0.5 * 0.5 * 0.3
ALB_NESTCOEF= 1.0 * 0.5 * 0.3
APR_NESTCOEF= 0.5 * 0.5 * 0.3
endif

; Market Segmentation
  PSHORT=   ZI.2.PSHORTMD[I]
  PLONG=    ZI.2.PLONGMD[I]

  MW[302]=MW[3]                                                             ; Auto 1
       MW[321]=MW[302] * PSHORT * ZI.2.ASHORTMD[J]                          ; P Short Walk - A Short Walk
       MW[322]=MW[302] * PSHORT * ZI.2.ALONGMD[J]                           ; P Short Walk - A Long Walk
       MW[323]=MW[302] * PLONG * ZI.2.ASHORTMD[J]                           ; P Long Walk - A Short Walk
       MW[324]=MW[302] * PLONG * ZI.2.ALONGMD[J]                            ; P Long Walk - A Long Walk
       MW[325]=MW[302] * (1 - PSHORT - PLONG) * ZI.2.ASHORTMD[J]            ; Auto - A Short Walk
       MW[326]=MW[302] * (1 - PSHORT - PLONG) * ZI.2.ALONGMD[J]             ; Auto - A Long Walk
       MW[327]=MW[302]-(MW[321]+MW[322]+MW[323]+MW[324]+MW[325]+MW[326])  ; No Transit


;; USE MW[501] TO MW[507] FOR WALKPATRAN
MW[501]=ZI.2.PRDSHTMD[I]+ZI.2.ATTSHTMD[J]
MW[502]=ZI.2.PRDSHTMD[I]+ZI.2.ATTLNGMD[J]
MW[503]=ZI.2.PRDLNGMD[I]+ZI.2.ATTSHTMD[J]
MW[504]=ZI.2.PRDLNGMD[I]+ZI.2.ATTLNGMD[J]
MW[505]=999.9
MW[506]=999.9
MW[507]=999.9
;; USE MW[511] TO MW[517] FOR WALKPAPR
MW[511]=ZI.4.STWALKPR[I]+ZI.2.ATTSHTMD[J]
MW[512]=ZI.4.STWALKPR[I]+ZI.2.ATTLNGMD[J]
MW[513]=ZI.4.STWALKPR[I]+ZI.2.ATTSHTMD[J]
MW[514]=ZI.4.STWALKPR[I]+ZI.2.ATTLNGMD[J]
MW[515]=ZI.4.STWALKPR[I]+ZI.2.ATTSHTMD[J]
MW[516]=ZI.4.STWALKPR[I]+ZI.2.ATTLNGMD[J]
MW[517]=999.9

LOOP ACC=1,7

; Zonal Variables
  PCODWLK=  ZI.2.CODWLKMD[I]
  PTERMTIME=ZI.3.TERMTIME[I]
  PCBD=     ZI.3.CBD[I]
  PEXURB=   ZI.3.EXURB[I]


; ===================================================================================
; NHB TRIP PURPOSE
; ===================================================================================

  ;   TOTAL AUTO PERSONS/DRIVE ALONE ELEMENTS OF UTILITY ARE:
      MW[11]=TERMCOEF*(ptermtime+ZI.3.TERMTIME[J]) +
            AAUTORUNC*(MI.2.CGTIME-MI.2.CGTOLL*CTOLL*60.0) +
            ACOSTC*(AOC*MI.2.CGDIST+MI.2.CGTOLL*100)*AUTOINFC +
            PARKC*ZI.3.PKCOST24[J]*0.5*PARKINFC +
            AU1DTC*(PCBD+ZI.3.CBD[J]) + AU1XPC*PEXURB + AU1XAC*ZI.3.EXURB[J]
  ;   COMPOSITE UTILITY
      MW[021]=(MW[11]+K_DA_AUTO0)/DA_NESTCOEF
      MW[031]=(MW[11]+K_DA_AUTO1)/DA_NESTCOEF
      MW[041]=(MW[11]+K_DA_AUTO2)/DA_NESTCOEF


  ;   SHARED RIDE PERSONS/SHARE RIDE - ONE PASSENGER ELEMENTS OF UTILITY ARE:
      MW[11]=TERMCOEF*(ptermtime+ZI.3.TERMTIME[J]) +
            AAUTORUNC*(MI.3.HOVCGTIME-MI.3.HOVCGTOLL*CTOLL*60.0) +
            HOVDIFFC*MAX(((MI.3.HOVCGTIME-MI.3.HOVCGTOLL*CTOLL*60.0) -
           (MI.2.CGTIME-MI.2.CGTOLL*CTOLL*60.0)),0) +
           (ACOSTC*(AOC*MI.3.HOVCGDIST+MI.3.HOVCGTOLL*100.0)*AUTOINFC)/OC2 +
           (PARKC*ZI.3.PKCOST24[J]*0.5*PARKINFC)/OC2 +
           (AU2DTC*(PCBD+ZI.3.CBD[J])) + (AU2XPC*PEXURB) + (AU2XAC*ZI.3.EXURB[J])
  ;   COMPOSITE UTILITY
      MW[022]=(MW[11]+K_SR1PASS_AUTO0)/SR_NESTCOEF
      MW[032]=(MW[11]+K_SR1PASS_AUTO1)/SR_NESTCOEF
      MW[042]=(MW[11]+K_SR1PASS_AUTO2)/SR_NESTCOEF


  ;   SHARED RIDE - 2+ PASSENGER ELEMENTS OF UTILITY ARE:
      MW[11]=TERMCOEF*(ptermtime+ZI.3.TERMTIME[J]) +
            AAUTORUNC*(MI.3.HOVCGTIME-MI.3.HOVCGTOLL*CTOLL*60.0) +
            HOVDIFFC*MAX(((MI.3.HOVCGTIME-MI.3.HOVCGTOLL*CTOLL*60.0) -
           (MI.2.CGTIME-MI.2.CGTOLL*CTOLL*60.0)),0) +
           (ACOSTC*(AOC*MI.3.HOVCGDIST+MI.3.HOVCGTOLL*100.0)*AUTOINFC)/OC3 +
           (PARKC*ZI.3.PKCOST24[J]*0.5*PARKINFC)/OC3 + 
           (AU3DTC*(PCBD+ZI.3.CBD[J])) + (AU3XPC*PEXURB) + (AU3XAC*ZI.3.EXURB[J])
  ;   COMPOSITE UTILITY
      MW[023]=(MW[11]+K_SR2PASS_AUTO0)/SR_NESTCOEF
      MW[033]=(MW[11]+K_SR2PASS_AUTO1)/SR_NESTCOEF
      MW[043]=(MW[11]+K_SR2PASS_AUTO2)/SR_NESTCOEF


  ;   TOTAL TRANSIT/ WALK ACCESS/ WALK ACCESS - LOCAL BUS ELEMENTS OF UTILITY ARE:
      MW[11]=WALKC*(MW[500+ACC] + MAX((MI.4.WALKTIME-PCODWLK-ZI.2.CODWLKMD[J]),0.0)) + 
            WTTAC*(MIN(MI.4.IWAIT,7.0)) +
            WTTBC*(MAX((MI.4.IWAIT-7.0),0.0)) +
            XFTC*MI.4.XWAIT + NXFC*MI.4.XFERS + 
            FAREC*MI.4.FARE*100*FAREINFC +
            TWLDTC*(PCBD+ZI.3.CBD[J]) + TWLXPC*PEXURB + TWLXAC*ZI.3.EXURB[J]
      MW[12]=TRUNC*(MI.4.LBTIME+MI.4.EBTIME+MI.4.RLTIME)
      JLOOP
        IF (MW[12]=0 || MI.4.LBTIME=0) MW[12]=-999.99
      ENDJLOOP
  ;   COMPOSITE UTILITY
      MW[024]=(MW[11]+MW[12]+K_WALKLOC_AUTO0)/WLB_NESTCOEF
      MW[034]=(MW[11]+MW[12]+K_WALKLOC_AUTO1)/WLB_NESTCOEF
      MW[044]=(MW[11]+MW[12]+K_WALKLOC_AUTO2)/WLB_NESTCOEF


  ;   WALK ACCESS (PREMIUM / EXPRS BUS)ELEMENTS OF UTILITY ARE:
      MW[11]=WALKC*(MW[500+ACC] + MAX((MI.5.WALKTIME-PCODWLK-ZI.2.CODWLKMD[J]),0.0)) + 
            WTTAC*(MIN(MI.5.IWAIT,7.0)) +
            WTTBC*(MAX((MI.5.IWAIT-7.0),0.0)) +
            XFTC*MI.5.XWAIT + NXFC*MI.5.XFERS + 
            FAREC*MI.5.FARE*100*FAREINFC +
            TWEDTC*(PCBD+ZI.3.CBD[J]) + TWEXPC*PEXURB + TWEXAC*ZI.3.EXURB[J]
      MW[12]=TRUNC*(MI.5.LBTIME+MI.5.EBTIME+MI.5.RLTIME)
      JLOOP
        IF (MW[12]=0 || MI.5.EBTIME=0) MW[12]=-999.99
      ENDJLOOP
  ;   COMPOSITE UTILITY
      MW[025]=(MW[11]+MW[12]+K_WLKPREB_AUTO0)/WPR_NESTCOEF
      MW[035]=(MW[11]+MW[12]+K_WLKPREB_AUTO1)/WPR_NESTCOEF
      MW[045]=(MW[11]+MW[12]+K_WLKPREB_AUTO2)/WPR_NESTCOEF


  ;   WALK ACCESS - RAIL ELEMENTS OF UTILITY ARE:
      MW[11]=WALKC*(MW[500+ACC] + MAX((MI.6.WALKTIME-PCODWLK-ZI.2.CODWLKMD[J]),0.0)) + 
            WTTAC*(MIN(MI.6.IWAIT,7.0)) +
            WTTBC*(MAX((MI.6.IWAIT-7.0),0.0)) +
            XFTC*MI.6.XWAIT + NXFC*MI.6.XFERS + 
            FAREC*MI.6.FARE*100*FAREINFC +
            TWRDTC*(PCBD+ZI.3.CBD[J]) + TWRXPC*PEXURB + TWRXAC*ZI.3.EXURB[J]
      MW[12]=TRUNC*(MI.6.LBTIME+MI.6.EBTIME+MI.6.RLTIME)
      JLOOP
        IF (MW[12]=0 || MI.6.RLTIME=0) MW[12]=-999.99
      ENDJLOOP
  ;   COMPOSITE UTILITY
      MW[026]=(MW[11]+MW[12]+K_WLKPRER_AUTO0)/WPR_NESTCOEF
      MW[036]=(MW[11]+MW[12]+K_WLKPRER_AUTO1)/WPR_NESTCOEF
      MW[046]=(MW[11]+MW[12]+K_WLKPRER_AUTO2)/WPR_NESTCOEF


  ;   AUTO ACCESS/AUTO ACCESS - LOCAL BUS ELEMENTS OF UTILITY ARE:
      MW[11]=WALKC*(MW[510+ACC] + MAX((MI.7.WALKTIME-ZI.2.CODWLKMD[J]),0.0)) + 
            AAUTORUNC*MI.7.AUTOTIME +
            WTTAC*(MIN(MI.7.IWAIT,7.0)) +
            WTTBC*(MAX((MI.7.IWAIT-7.0),0.0)) +
            XFTC*MI.7.XWAIT + NXFC*MI.7.XFERS +
            FAREC*(MI.7.FARE*100+(.5*ZI.4.STPKCS2/OCTA))*FAREINFC +
            ACOSTC*(MI.7.AUTOTIME/60.0*TASPD*AOC/OCTA)*AUTOINFC +
            TALDTC*(PCBD+ZI.3.CBD[J]) + TALXPC*PEXURB + TALXAC*ZI.3.EXURB[J]
      MW[12]=TRUNC*(MI.7.LBTIME+MI.7.EBTIME+MI.7.RLTIME)
      JLOOP
        IF (MW[12]=0 || MI.7.LBTIME=0) MW[12]=-999.99
      ENDJLOOP
  ;   COMPOSITE UTILITY
      MW[027]=(MW[11]+MW[12]+K_AUTOLOC_AUTO0)/ALB_NESTCOEF
      MW[037]=(MW[11]+MW[12]+K_AUTOLOC_AUTO1)/ALB_NESTCOEF
      MW[047]=(MW[11]+MW[12]+K_AUTOLOC_AUTO2)/ALB_NESTCOEF


  ;   AUTO ACCESS (PREMIUM/EXPRS BUS) ELEMENTS OF UTILITY ARE:
      MW[11]=WALKC*(MW[510+ACC] + MAX((MI.8.WALKTIME-ZI.2.CODWLKMD[J]),0.0)) + 
            AAUTORUNC*MI.8.AUTOTIME +
            WTTAC*(MIN(MI.8.IWAIT,7.0)) +
            WTTBC*(MAX((MI.8.IWAIT-7.0),0.0)) +
            XFTC*MI.8.XWAIT + NXFC*MI.8.XFERS +
            FAREC*(MI.8.FARE*100+(.5*ZI.4.STPKCS2/OCTA))*FAREINFC +
            ACOSTC*(MI.8.AUTOTIME/60.0*TASPD*AOC/OCTA)*AUTOINFC +
            TAEDTC*(PCBD+ZI.3.CBD[J]) + TAEXPC*PEXURB + TAEXAC*ZI.3.EXURB[J]
      MW[12]=TRUNC*(MI.8.LBTIME+MI.8.EBTIME+MI.8.RLTIME)
      JLOOP
        IF (MW[12]=0 || MI.8.EBTIME=0) MW[12]=-999.99
      ENDJLOOP
  ;   COMPOSITE UTILITY
      MW[028]=(MW[11]+MW[12]+K_AUTOPREB_AUTO0)/APR_NESTCOEF
      MW[038]=(MW[11]+MW[12]+K_AUTOPREB_AUTO1)/APR_NESTCOEF
      MW[048]=(MW[11]+MW[12]+K_AUTOPREB_AUTO2)/APR_NESTCOEF


  ;   AUTO ACCESS - RAIL ELEMENTS OF UTILITY ARE:
      MW[11]=WALKC*(MW[510+ACC] + MAX((MI.9.WALKTIME-ZI.2.CODWLKMD[J]),0.0)) + 
            AAUTORUNC*MI.9.AUTOTIME +
            WTTAC*(MIN(MI.9.IWAIT,7.0)) +
            WTTBC*(MAX((MI.9.IWAIT-7.0),0.0)) +
            XFTC*MI.9.XWAIT + NXFC*MI.9.XFERS +
            FAREC*(MI.9.FARE*100+(.5*ZI.4.STPKCS2/OCTA))*FAREINFC +
            ACOSTC*(MI.9.AUTOTIME/60.0*TASPD*AOC/OCTA)*AUTOINFC +
            TARDTC*(PCBD+ZI.3.CBD[J]) + TARXPC*PEXURB + TARXAC*ZI.3.EXURB[J]
      MW[12]=TRUNC*(MI.9.LBTIME+MI.9.EBTIME+MI.9.RLTIME)
      JLOOP
        IF (MW[12]=0 || MI.9.RLTIME=0) MW[12]=-999.99
      ENDJLOOP
  ;   COMPOSITE UTILITY
      MW[029]=(MW[11]+MW[12]+K_AUTORAIL_AUTO0)/APR_NESTCOEF
      MW[039]=(MW[11]+MW[12]+K_AUTORAIL_AUTO1)/APR_NESTCOEF
      MW[049]=(MW[11]+MW[12]+K_AUTORAIL_AUTO2)/APR_NESTCOEF
/*
; NHB (AUTO0)
      DMD=ACC+310
      XCHOICE ALTERNATIVES=DA SR1PASS SR2PASS WALKLOC WLKPREB WLKPRER AUTOLOC AUTOPREB AUTORAIL,
      UTILITIESMW=21,22,23,24,25,26,27,28,29,
      DEMANDMW=DMD,
      ODEMANDMW=401,402,403,404,405,406,407,408,409,
      STARTMW=520,
;     Model Structure
;     Top level nest
      SPLIT=TOTAL  0.8 TOTAUTOS 0.3 TRANSIT,
;     Totautos nest
      SPLIT=TOTAUTOS  1.0  DA  0.2  SHRIDE,
;     Shride nest
      SPLIT=SHRIDE   1.0 SR1PASS 1.0 SR2PASS,
;     Transit nest
      SPLIT= TRANSIT  0.5 WALKACC 0.5 AUTOACC,
;     Walkacc nest
      SPLIT= WALKACC  1.0 WALKLOC 0.5 WALKPRE,
;     Walkpre nest
      SPLIT= WALKPRE  1.0 WLKPREB 1.0 WLKPRER,
;     Autoacc nest
      SPLIT= AUTOACC  1.0 AUTOLOC 0.5 AUTOPRE,
;     Autopre nest
      SPLIT= AUTOPRE  1.0 AUTOPREB 1.0 AUTORAIL
*/
; NHB (AUTO1)
      DMD=ACC+320
      XCHOICE ALTERNATIVES=DA SR1PASS SR2PASS WALKLOC WLKPREB WLKPRER AUTOLOC AUTOPREB AUTORAIL,
      DEMANDMW=DMD,
      UTILITIESMW=31,32,33,34,35,36,37,38,39,
      ODEMANDMW=411,412,413,414,415,416,417,418,419,
      STARTMW=520,
;     Model Structure
;     Top level nest
      SPLIT=TOTAL  0.8 TOTAUTOS 0.3 TRANSIT,
;     Totautos nest
      SPLIT=TOTAUTOS   1.0 DA  0.2  SHRIDE,
;     Shride nest
      SPLIT=SHRIDE   1.0 SR1PASS 1.0 SR2PASS,
;     Transit nest
      SPLIT= TRANSIT  0.5 WALKACC 0.5 AUTOACC,
;     Walkacc nest
      SPLIT= WALKACC  1.0 WALKLOC 0.5 WALKPRE,
;     Walkpre nest
      SPLIT= WALKPRE  1.0 WLKPREB 1.0 WLKPRER,
;     Autoacc nest
      SPLIT= AUTOACC  1.0 AUTOLOC 0.5 AUTOPRE,
;     Autopre nest
      SPLIT= AUTOPRE  1.0 AUTOPREB 1.0 AUTORAIL

/*
;NHB (AUTO2)
      DMD=ACC+330
      XCHOICE ALTERNATIVES=DA SR1PASS SR2PASS WALKLOC WLKPREB WLKPRER AUTOLOC AUTOPREB AUTORAIL,
      DEMANDMW=DMD,
      UTILITIESMW=41,42,43,44,45,46,47,48,49,
      ODEMANDMW=421,422,423,424,425,426,427,428,429,
      STARTMW=520,
;     Model Structure
;     Top level nest
      SPLIT=TOTAL  0.8 TOTAUTOS 0.3 TRANSIT,
;     Totautos nest
      SPLIT=TOTAUTOS  1.0 DA  0.2  SHRIDE,
;     Shride nest
      SPLIT=SHRIDE  1.0 SR1PASS 1.0 SR2PASS,
;     Transit nest
      SPLIT= TRANSIT  0.5 WALKACC 0.5 AUTOACC,
;     Walkacc nest
      SPLIT= WALKACC  1.0 WALKLOC 0.5 WALKPRE,
;     Walkpre nest
      SPLIT= WALKPRE  1.0 WLKPREB 1.0 WLKPRER,
;     Autoacc nest
      SPLIT= AUTOACC  1.0 AUTOLOC 0.5 AUTOPRE,
;     Autopre nest
      SPLIT= AUTOPRE  1.0 AUTOPREB 1.0 AUTORAIL
*/

MW[141]=MW[141]+MW[411];+MW[401]+MW[421]                     ; DA          X
MW[142]=MW[142]+MW[412];+MW[402]+MW[422]                     ; SR1PASS     X
MW[143]=MW[143]+MW[413];+MW[403]+MW[423]                     ; SR2PASS     X
MW[144]=MW[144]+MW[414];+MW[404]+MW[424]                     ; WALKLOC     X
MW[145]=MW[145]+MW[415];+MW[405]+MW[425]                     ; WLKPREB     X
MW[146]=MW[146]+MW[416];+MW[406]+MW[426]                     ; WLKPRER     X
MW[147]=MW[147]+MW[417];+MW[407]+MW[427]                     ; AUTOLOC     X
MW[148]=MW[148]+MW[418];+MW[408]+MW[428]                     ; AUTOPREB    X
MW[149]=MW[149]+MW[419];+MW[409]+MW[429]                     ; AUTORAIL    X

ENDLOOP

; Assign intrazonal from SR1PASS and SR2PASS to DA
INTRAZONAL MW[141]=MW[141]+MW[142]+mw[143]
INTRAZONAL MW[142]=0
INTRAZONAL MW[143]=0
; OUTPUT
MW[151]=MW[141]+MW[142]+MW[143]                             ; TOTAUTOS
MW[152]=MW[141]                                             ; DA          X
MW[153]=MW[142]+MW[143]                                     ; SHRIDE
MW[154]=MW[142]                                             ; SR1PASS     X
MW[155]=MW[143]                                             ; SR2PASS     X
MW[156]=MW[144]+MW[145]+MW[146]+MW[147]+MW[148]+MW[149]     ; TRANSIT
MW[157]=MW[144]+MW[145]+MW[146]                             ; WALKACC     
MW[158]=MW[144]                                             ; WALKLOC     X
MW[159]=MW[145]+MW[146]                                     ; WALKPRE     
MW[160]=MW[145]                                             ; WLKPREB     X
MW[161]=MW[146]                                             ; WLKPRER     X
MW[162]=MW[147]+MW[148]+MW[149]                             ; AUTOACC
MW[163]=MW[147]                                             ; AUTOLOC     X
MW[164]=MW[148]+MW[149]                                     ; AUTOPRE
MW[165]=MW[148]                                             ; AUTOPREB    X
MW[166]=MW[149]                                             ; AUTORAIL    X


ENDRUN
